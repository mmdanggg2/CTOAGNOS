;-----	Console -----
:console ;For later inits
set sp, 0x0000
set push, a
set push, b

set a, 0
set b, [screen_loc]
hwi [dev_screen]

set a, 1
set b, sys_font ;Set font
hwi [dev_screen]

set a, 2
set b, sys_colour ;Set colours
hwi [dev_screen]

set [colour_text], [colour_text_user]	;Change to user settings
set [colour_border], [colour_border_user]
jsr border_colour

set [char_x_start], 0x0001
set [con_cmd_pos], 0x0000
jsr console_clear_cmd
jsr clear_screen

set [char_x], 0x0000
set [char_y], 0x0000
set a, intro_text
jsr draw_string

set [char_x], 0x0000
set [char_y], 0x0001
jsr draw_cmd_line

set b, pop
set a, pop
set pc, console_loop

:console_loop
jsr key_inp
ifn c, 0x0000	;if input
	set pc, console_inp
set pc, console_loop

:console_new_cmd
set push, a
jsr get_screen_offset
set a, [char_offset]
ifn [a], 0
	jsr char_next_line
ifn [char_x], [char_x_start]
	jsr char_next_line
set [char_x], 0x0000
jsr console_clear_cmd
jsr draw_cmd_line
set a, pop
set pc, console_loop

;-- Input Handling --

:console_inp
ife c, 0x0010
	set pc, console_bksp	;Backspace
ife c, 0x0011
	set pc, console_return	;Return
ife c, 0x0012
	set pc, console_insert	;Insert
ife c, 0x0013
	set pc, console_delete	;Delete
set pc, console_char	;Other

:console_bksp
ife [char_x], [char_x_start]	;if nothing to remove
	set pc, console_loop		;return
jsr draw_blank
sub [char_x], 0x0001
jsr draw_cur
set push, i
ifn [con_cmd_pos], 0x0000
	sub [con_cmd_pos], 0x0001
set i, [con_cmd_pos]
set [con_cmd+i], 0x0000
set i, pop
set pc, console_loop

:console_return
jsr draw_blank
jsr char_next_line
set pc, console_cmd_read

:console_insert
set push, a
set a, con_cmd
jsr draw_string
jsr draw_cur
set a, pop
set pc, console_loop

:console_delete
set push, c
set c, 0x4D
jsr draw_char
jsr draw_cur
set c, pop
set pc, console_loop

;-- Character Handling --

:console_char
set push, [colour_cur]
jsr draw_char
jsr char_next
ife [char_x], 0x001F
	set [colour_cur], 0xC000
jsr console_cmd_inp
jsr draw_cur
set [colour_cur], pop
set pc, console_loop

:console_cmd_inp
ife [con_cmd_pos], [con_cmd_len]
	set pc, pop
set push, i
set i, [con_cmd_pos]
set [con_cmd+i], c
add [con_cmd_pos], 0x0001
set i, pop
set pc, pop

;-- Command Reading --

:console_cmd_read
ife [con_cmd_pos], 0x0000
	set pc, console_new_cmd
set push, i
set push, a
set push, b
set i, 0x0000
:console_cmd_read_loop
ife [cmd_list+i], 0x0000
	set pc, console_cmd_read_none
set a, [cmd_list+i]
set b, con_cmd
jsr cmp_string
ife z, 0x0001
	set pc, console_cmd_read_match
add i, 0x0001
set pc, console_cmd_read_loop

:console_cmd_read_none
set b, pop
set a, pop
set i, pop
set push, a
set [char_x], 0x0000
set a, not_cmd
jsr draw_string
set a, pop
set pc, console_new_cmd

:console_cmd_read_match
set b, pop
set a, pop
set [null], pop
jsr console_clear_cmd
set [con_cmd_pos], 0x0000
set pc, [cmd_list_addr+i]

:console_clear_cmd
set push, i
set push, z
set push, y
set i, con_cmd
set z, [con_cmd_len]
set y, 0
jsr fill_mem
set [con_cmd_pos], 0
set y, pop
set z, pop
set i, pop
set pc, pop

;-- End loop --

:end_secret
set [char_x], 0
set [char_y], 0
set a, secret
set i, 0x0000
set j, 0x0000
:end_secret_loop
add i, 0x0001
ife i, 0xFFFF
	add j, 0x0001
ife j, 0x0004
	jsr draw_string
ife j, 0x0004
	set pc, end
set pc, end_secret_loop